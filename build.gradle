buildscript {
  repositories {
    jcenter()
    maven {
      url "http://repo.spring.io/milestone"
    }
  }

  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
    classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17")
    classpath("org.ajoberstar:gradle-git:1.7.2")
  }
}

plugins {
  id "io.spring.dependency-management" version "1.0.3.RELEASE"
  id "org.ajoberstar.grgit" version "1.7.2"
  id "org.ajoberstar.release-opinion" version "1.7.2"
}

allprojects {
  apply plugin: "idea"

  repositories {
    jcenter()
    maven {
      url "http://repo.spring.io/milestone"
    }
  }
}

idea {
  project {
    jdkName = "1.8"
    languageLevel = "1.8"
    vcs = "Git"
  }
}

configure(subprojects) {
  apply plugin: "java"
  apply plugin: "checkstyle"
  apply plugin: "findbugs"
  apply plugin: "pmd"
  apply plugin: "io.spring.dependency-management"
  apply plugin: "com.gorylenko.gradle-git-properties"

  group "com.petercoulton.bluecowmoon"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencyManagement {
    imports {
      mavenBom "io.spring.platform:platform-bom:${spring_platform_version}"
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
    }
  }

  dependencies {
    testCompile("org.springframework.boot:spring-boot-starter-test")
  }

  checkstyle {
    config = rootProject.file("codequality/checkstyle/checkstyle.xml")
  }

  tasks.withType(Pmd) {
    reports.html.enabled = true
  }
}


task wrapper(type: Wrapper) {
  gradleVersion = '4.0'
}